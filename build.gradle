import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1' apply false
    id 'io.spring.dependency-management' version '1.1.5' apply false
}

// 모든 프로젝트(루트 + 하위)에 공통으로 적용될 설정
allprojects {
    group = 'com.lightningbid'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

// 모든 하위 프로젝트에 공통으로 적용될 설정
subprojects {
    // 모든 하위 모듈은 Java 프로젝트
    apply plugin: 'java'
    // Spring 의존성 관리 플러그인 적용
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        toolchain.languageVersion = JavaLanguageVersion.of(21)
    }

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
    }

    // 모든 하위 모듈에 공통으로 필요한 의존성
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }
}


jar.enabled = false